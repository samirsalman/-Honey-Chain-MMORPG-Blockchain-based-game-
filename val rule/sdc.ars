DEFINE urlset inchiaro = { 
    /img/*, //*/
    /bootstrap/*/*,    //*/
    /json/* //*/
};


DEFINE set urlinchiaro = { "/", "/index.html" , "/login.html" };


//		-----------------------------------
//			FASE DI REGISTRAZIONE
//		-----------------------------------


DEFINE set email_esistenti = 
   : MySql{127.0.0.1:3306, "honey", "root", "password", "select email from user"};

DEFINE AR "verifica_registrazione_email_non_valida"
    CONDITION
		http.url is "/user/register"
		exists http.query["email"]								
        	http.query["email"] is in email_esistenti "%40" "@"									 
	ACTION
		MANAGE "REGISTRATION_ERROR_MAIL"
	;

DEFINE AR "calcolo_hash"
	CONDITION
		http.url is "/user/register"
		exists http.query["email"]
        	!http.query["email"] is in email_esistenti "%40" "@"
		exists http.query["password"]
		exists http.query["name"]
		exists http.query["years"]
	ACTION
		MANAGE "HASHPASS"
	;

DEFINE VR "aggiungi_utente_db_mysql"
    CONDITION
		http.url is "/user/hashregister"
		exists http.query["email"]
        	!http.query["email"] is in email_esistenti "%40" "@"
		exists http.query["password"]
		exists http.query["name"]
		exists http.query["years"] 									 
	VAR
		email = http.query["email"] "%40" "@"
		pass = http.query["password"]
		name = http.query["name"]
		years = http.query["years"]
	ACTION
		ADD http.query["email"] to set email_esistenti "%40" "@"
		ADD CAT{http.query["email"], http.query["password"]} to set utenti_registrati
		MySql.append (127.0.0.1:3306, "honey", "root", "password", "insert into user (email, password, name, years) values ($0, $1, $2, $3)", {email, pass, name, years})
		REPORT registrazioni {CAT {"***Nuova registrazione effettuata***\nEmail: ", email,"\nPassword: ", pass, "\nNome: " , name, "\nAnni: " , years}}
	;

DEFINE AR "registrazione_buona"
	CONDITION
		http.url is "/user/hashregister"
		exists http.query["email"]
        	!http.query["email"] is in email_esistenti "%40" "@"
		exists http.query["name"]
		exists http.query["years"]
	ACTION
		MANAGE "SENDDATAREG"
	;

//		-------------------------------------
//			FASE DI LOGIN NEL GIOCO
//		-------------------------------------

DEFINE set utenti_registrati = 
   : MySql{127.0.0.1:3306, "honey", "root", "password", "select CONCAT(email, password) as utenti from user"};

DEFINE AR "verifica_login_senza_cookie_email_non_valida_game"
    CONDITION
        http.url is "/user/loginGame"
        !exists http.cookie["login"]
        exists http.query["email"]
        !http.query["email"] is in email_esistenti "%40" "@"
    ACTION
        MANAGE "LOGIN_COOKIE_ERROR"
    ;

DEFINE AR "verifica_login_senza_cookie_email_valida_game"
    CONDITION
        http.url is "/user/loginGame"
        !exists http.cookie["login"]
        exists http.query["email"]
        http.data["email"] is in email_esistenti "%40" "@"
        exists http.query["password"]
    ACTION
        MANAGE "LOGIN_GAME"
    ;

DEFINE AR "verifica_login_con_cookie_non_valido_game"
    CONDITION
        http.url is "/user/loginGame"
        exists http.cookie["login"]
        !http.cookie["login"] is in cookie_for_login
    ACTION
        answer "Cookie non valido"
    ;

DEFINE AR "passa_login_game"
    CONDITION
        http.url is "/user/loginGame"
        exists http.cookie["login"]
        http.cookie["login"] is in cookie_for_login
    ACTION
        MANAGE "DATA_GAME"
    ;

DEFINE AR "setta_cookie_login_game"
    CONDITION
        http.url is "/controlloGame"
        exists http.query["email"]
        exists http.query["password"]
        CAT{http.query["email"], http.query["password"]} is in utenti_registrati
        !exists http.cookie["login"]
    ACTION
        MANAGE "SETTO_COOKIE_LOGIN_GAME"
    ;

DEFINE VR "verifica_login_e_aggiungi_cookie_game"
    CONDITION
        obs.event is net.send
        http.url is "/controlloGame"
        exists http.query["email"]
        exists http.query["password"]
        CAT{http.query["email"], http.query["password"]} is in utenti_registrati
        !exists http.cookie["login"]
    VAR
        email = http.query["email"] "%40" "@"
        v_sid = net.sesid
    ACTION
        REPORT loginGame {CAT { "---Tentativo di Login nel gioco---\nEmail: " , email, "\nSessione: ", v_sid, "\nIP: ", net.ipsrc ":.*" "", "\nTime: ", obs.time}}
    NEXT 
    (
            obs.event is net.recv
            http.answer.code is "302 Found"
            net.sesid is v_sid
            exists http.answer.cookie["login"]
        ACTION
            ADD CAT{http.answer.cookie["login"] "[;].*" ""} to set cookie_for_login 180
            MySql.append (127.0.0.1:3306, "honey", "root", "password", "insert into report_login (email, cookie, sessione, ip, time) values ($0, $1, $2, $3, $4)", {email, http.answer.cookie["login"], v_sid, net.ipsrc, obs.time})
            REPORT loginGame {CAT {"***Login avvenuto con successo***\nEmail: ", email, "\nCookie_Log: ", http.answer.cookie["login"] "[;].*" "", "\nSessione: ", v_sid, "\nIP: ", net.ipsrc ":.*" "", "\nTime: ", obs.time}}
        OR
            obs.event is net.recv
            net.sesid is v_sid
            !exists http.answer.cookie["login"]
        ACTION
            REPORT loginGame {CAT {"***Login fallito***\nEmail: ", email, "\nSessione: ", v_sid ,"\nIP: ", net.ipsrc ":.*" "", "\nTime: ", obs.time}}
    )
    ;

DEFINE AR "passa_login_con_cookie_game"
    CONDITION
        http.url is "/controlloGame"
        exists http.cookie["login"]
    ACTION
        MANAGE "LOG_GIUSTO"
    ;


//		-------------------------------------
//			FASE DI LOGIN IN APP
//		-------------------------------------

DEFINE set cookie_for_login = { };

DEFINE AR "verifica_login_senza_cookie_email_non_valida"
	CONDITION
		http.url is "/user/login"
		!exists http.cookie["login"]
		exists http.query["email"]
		!http.query["email"] is in email_esistenti "%40" "@"
	ACTION
		MANAGE "LOGIN_ERROR_MAIL"
	;

DEFINE AR "verifica_login_senza_cookie_email_valida"
	CONDITION
		http.url is "/user/login"
		!exists http.cookie["login"]
		exists http.query["email"]
		http.data["email"] is in email_esistenti "%40" "@"
		exists http.query["password"]
	ACTION
		MANAGE "LOGIN"
	;

DEFINE AR "verifica_login_con_cookie_non_valido"
	CONDITION
		http.url is "/user/login"
		exists http.cookie["login"]
		!http.cookie["login"] is in cookie_for_login
	ACTION
		MANAGE "COOKIE_ERROR"
	;

DEFINE AR "passa_login_app"
	CONDITION
		http.url is "/user/login"
		exists http.cookie["login"]
		http.cookie["login"] is in cookie_for_login
	ACTION
		MANAGE "DATA"
	;

DEFINE AR "setta_cookie_login"
	CONDITION
		http.url is "/verificaDati"
		exists http.query["email"]
		exists http.query["password"]
		CAT{http.query["email"], http.query["password"]} is in utenti_registrati
		!exists http.cookie["login"]
	ACTION
		MANAGE "SETTO_COOKIE_LOGIN"
	;

DEFINE VR "verifica_login_e_aggiungi_cookie"
	CONDITION
		obs.event is net.send
		http.url is "/verificaDati2"
		exists http.query["email"]
		exists http.query["password"]
		CAT{http.query["email"], http.query["password"]} is in utenti_registrati
		exists http.query["cookie"]
		!exists http.cookie["login"]
	VAR
		email = http.query["email"] "%40" "@"
		cook = http.query["cookie"]
		v_sid = net.sesid
	ACTION
		REPORT loginApp {CAT { "---Tentativo di Login---\nEmail: " , email, "\nSessione: ", v_sid, "\nIP: ", net.ipsrc ":.*" "", "\nTime: ", obs.time}}
	NEXT 
	(
			obs.event is net.recv
			http.answer.code is "302 Found"
			net.sesid is v_sid
		ACTION
			ADD CAT{cook "[;].*" ""} to set cookie_for_login 180
			MySql.append (127.0.0.1:3306, "honey", "root", "password", "insert into report_login (email, cookie, sessione, ip, time) values ($0, $1, $2, $3, $4)", {email, cook, v_sid, net.ipsrc, obs.time})
			REPORT loginApp {CAT {"---Login avvenuto con successo---\nEmail: ", email, "\nCookie_Log: ", cook "[;].*" "", "\nSessione: ", v_sid, "\nIP: ", net.ipsrc ":.*" "", "\nTime: ", obs.time}}
		OR
			obs.event is net.recv
			net.sesid is v_sid
			!exists http.query["cookie"]
		ACTION
			REPORT loginApp {CAT {"---Login fallito---\nEmail: ", email, "\nSessione: ", v_sid ,"\nIP: ", net.ipsrc ":.*" "", "\nTime: ", obs.time}}
	)
	;

DEFINE AR "passa_login_con_cookie_app"
	CONDITION
		http.url is "/verificaDati2"
		exists http.query["cookie"]
	ACTION
		MANAGE "LOGIN_SUCCESS"
	;


//		--------------------------------------------
//			RICHIESTA OGGETTI e TRANSAZIONI
//		--------------------------------------------

DEFINE AR "post_login"
	CONDITION
		http.url is "/user/getObjects"
		exists http.query["email"]
	ACTION
		MANAGE "REDIRECT_SERVER"
	;

DEFINE AR "transaction"
	CONDITION
		http.url is "/user/transaction"
		exists http.query["email"]
		exists http.query["id"]
	ACTION
		MANAGE "TRANSACTION"
	;


DEFINE AR "history"
	CONDITION
		http.url is "/history"
		exists http.query["id"]
		exists http.query["email"]
	ACTION
		MANAGE "HISTORY"
	;

//		-------------------------------------------------
//			VERIFICA DI ACCESSO A NUOVI LIVELLI
//		-------------------------------------------------
	
DEFINE AR "accesso_piano_2"
	CONDITION
		http.url is "/levelup"
		http.query["floor"] is "2"
		http.query["point"] is "1"
	ACTION
		answer "ok"
	;
	
DEFINE AR "accesso_piano_3"
	CONDITION
		http.url is "/levelup"
		http.query["floor"] is "3"
		http.query["point"] is "1"
	ACTION
		answer "ok"
	;
	
DEFINE AR "accesso_piano_4"
	CONDITION
		http.url is "/levelup"
		http.query["floor"] is "4"
		http.query["point"] is "1"
	ACTION
		answer "ok"
	;
	
//		-------------------------------------
//			GIOCARE NUOVAMENTE
//		-------------------------------------

DEFINE AR "nuova_partita"
	CONDITION
		http.url is "/user/restart"
		exists http.cookie["login"]
		http.cookie["login"] is in cookie_for_login
		
	ACTION
		answer "Cookie presente"
	;

